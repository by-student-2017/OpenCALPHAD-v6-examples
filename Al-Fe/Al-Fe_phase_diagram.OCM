@$ ==================================================================
@$ calculate Al-Fe phase diagram including A2/B2 line in BCC
@$ ==================================================================
new Y
set echo Y

@$ ==================================================================
@$ Read TDB file (AlFe-4SLBF.TDB)
@$ ==================================================================
read tdb AlFe-4SLBF

@$ ==================================================================
@$ Calculate an equilibrium in the B2
@$ t=Temperature [K], p=Pressure [Pa], n=mol, x(Cu)=mole fraction of Cu
@$ ==================================================================
set condition t=1200 p=1e5 n=1 x(Al)=0.3

@$ ==================================================================
@$ one point calculation (Fix t, p, n and x)
@$ ==================================================================
calculate equilibrium

@$ ==================================================================
@$ Output of results (List)
@$ ==================================================================
@$ list , 2

@$ ==================================================================
@$ Change to condition in difference of y
@$ ==================================================================
set condition y(bcc,fe#3)-y(bcc,fe#2)
@$ list c

set condition x(al)=none
calculate equilibrium
@$ list ,,,,

@$ ==================================================================
@$ We can use "c e" as the gridminimizer cannot be used
@$ with these conditions
@$
@$ The phase has B2 ordering as two sublattices have equal and high Fe content
@$ and the other equal but lower Fe content.
@$
@$ Here the difference in Fe content is high
@$ We can to calculate a line when this difference is small
@$ First ensure we can set the difference as condition
@$ ==================================================================

@$ ==================================================================
@$ Change the value of the difference in y
@$ Note the previous difference is kept and must be removed explicitly
@$ That is easy using the notation specifying the condition number
@$ ==================================================================
set condition y(bcc,fe#3)-y(bcc,fe#2)=.5
@$ list c

set condition 4:=none
@$ ==================================================================
@$ We can use c e as gridmin is not possible with these conditions
@$ ==================================================================
calculate equilibrium
@$ list ,,,,

@$ ==================================================================
@$ The overall composition has changed as we are closer to the A2/B2 line
@$ Decrease the difference again
@$ ==================================================================
set condition y(bcc,fe#3)-y(bcc,fe#2)=.01
set condition 4:=none
@$ list c

@$ ==================================================================
@$ We are now sufficiently close to the A2/B2 transition line
@$ ==================================================================
calculate equilibrium
@$ list ,,,

@$ ==================================================================
@$ Set a T axis
@$ ==================================================================
set axis 1 T 300 2000 5

@$ ==================================================================
@$ step calculation
@$ ==================================================================
step

@$ ==================================================================
@$ plot the a2/b2 transition line
@$ ==================================================================
@$ plot x(Al) T
@$ title map 17 fig 1a

@$ ==================================================================
@$ Sometimes the lie stops at 1200 because the line up to the liquid
@$ is considered wrong.  Make sure it is included
@$ exclude (y), exclude (y), include (y), exclude (y)
@$ ==================================================================
amend line Y
Y
Y
Y

@$ ==================================================================
@$ plot as dashed line on a file to be appended
@$ ==================================================================
@$ plot x(Al) T
@$ extra line 0
@$ title map 17 fig 1b
@$ The final Y means overwrite any previous file
@$ output alfe-a2b2 Y

@$ ==================================================================
@$ set inter
@$ ==================================================================
@$ Now the Al-Fe phase diagram
@$ ==================================================================
new Y

@$ ==================================================================
@$ Read TDB file (AlFe-4SLBF.TDB)
@$ ==================================================================
read tdb AlFe-4SLBF

@$ ==================================================================
@$ Start with the High Al side
@$ ==================================================================

@$ ==================================================================
set c t=1000 p=1e5 n=1 x(al)=.52
@$ ==================================================================
calculate equilibrium
@$ list results 2

@$ ==================================================================
@$ Axis for thd diagram
@$ ==================================================================
set axis 1 x(Al) 0 1 0.01
set axis 2 t 300 2000 25

@$ ==================================================================
@$ Phase diagram calculation
@$ ==================================================================
map

@$ ==================================================================
@$ plot X(*,Al) T
@$ title map 17 fig 2a

@$ ==================================================================
@$ Mapping stops at B2/A2 transition in equilibrium with liquid
@$ That is an error which has to be fixed.  
@$ ==================================================================
set condition t=1900 p=1e5 n=1 x(al)=.3
calculate equilibrium
@$ list ,,,
map n

@$ ==================================================================
@$ plot X(*,Al) T
@$ title map 17 fig 2b

@$ ==================================================================
@$ The line liquid/BCC is still not complete,
@$ The part connectiong to the high Al curve was automatically
@$ excluded by OC as mapping stopped when the B2 became stable
@$ BUT we can restore that part!
@$ exclude (y), exclude (y), include (y), exclude (y)
@$ ==================================================================
amend line Y
Y
Y
Y

@$ ==================================================================
@$ plot , ,
@$ title map 17 fig 2c

@$ ==================================================================
@$ Note there are two  curves for liq/bcc around 50% Al.
@$ These can be removed by editing the ocgnu.plt file
@$ ==================================================================
@$ Now just the BCC/D03/B2 region missing
@$ ==================================================================
set condition t=600 x(al)=.25
calculate equilibrium
@$ list ,,,
map n

@$ ==================================================================
@$ Note that one composition set hase equal fractions in all 4
@$ sublattices whereas the other has high and equal fraction of Fe
@$ in 2 sublattices, less high fraction of Fe in the third and
@$ high fraction of Al in the fourth.  This is D0_3 ordering.
@$ ==================================================================
@$ The grid minimizer in OC must know that the BCC has B permutation to find
@$ this ordering miscibility gap.  With that information it generates
@$ gridpoints taking the ordering into account.
@$ ==================================================================

@$ ==================================================================
@$ plot X(*,Al) T
@$ title map 17 fig 2d

@$ ==================================================================
@$ Now the gamma loop
@$ ==================================================================
set condition x(al)=.015 t=1400
calculate equilibrium
@$ list ,,,,
map n

@$ ==================================================================
@$ Now plot everyting and append the already calculated and plotted a2/b2 curve
@$ ==================================================================
plot X(*,Al) T
axis y Temperature/K
append alfe-a2b2
title map 17 fig 3
@$ Label some areas (x, y, fontscale, angle, recalc, legend)
text 0.01 1400 0.9  0 y ,
text n 0.20 1900 0.9  0 y ,
text n 0.10 1400 0.9  0 y ,
text n 0.30 1200 0.9  0 y ,
text n 0.60 1400 0.9  0 y ,
text n 0.60  600 0.9 90 y ,
render

@$ ====================================================================
@$ PDF output settings
@$ (graph n; n=1: window, n=2:ps, n=3:pdf, n=4:gif, n=5:png)
@$ ====================================================================
plot ,,
graph 3 phase-diagram Y

@$ ==================================================================
@$ Surprise, all curves are dashed!
@$ You have to edit the ocgnu.plt file to have dashed line
@$ only for the A2/B2 line.  
@$
@$ Developing OC is a hobby for me,
@$ I have no ambition to solve all your problems calculating phase diagrams ...
@$ ==================================================================

@$ ==================================================================
@$ Calculating and adding the para/ferro magnetic transion
@$ along the Curie T curve is a task you can do yourself!
@$ If you want you can calculate and add the B2/D0_3 transition curve also.
@$ ==================================================================

@$ ==================================================================
@$ If you do not write this, you will get an error.
set interactive
@$ ==================================================================

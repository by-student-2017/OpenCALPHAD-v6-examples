@$ ==================================================================
@$  Mapping of isopleth in C-Cr-Fe
@$===================================================================
new Y
set echo Y

@$ ==================================================================
@$ Read TDB file (steel7.TDB)
@$ ==================================================================
read tdb steel7
Fe Cr C

@$ ==================================================================
@$ t=Temperature [K], p=Pressure [Pa], n=mol, w%(Cr)=weight %
@$ ==================================================================
set condition t=1200 p=1e5 n=1 w%(Cr)=13 w%(C)=1

@$ ==================================================================
@$ one point calculation (Fix t, p, n and x)
@$ ==================================================================
calculate equilibrium

@$ ==================================================================
@$ Output of results (List)
@$ ==================================================================
list results 1

@$ ==================================================================
@$ Axis definition (x-axis: weight %)
@$ ==================================================================
set axis 1 w%(c) 0 2 ,,

@$ ==================================================================
@$ Axis definition (y-axis: Temperature [K])
@$ ==================================================================
set axis 2 t 800 1800 ,,

@$ ==================================================================
@$ Phase diagram calculation
@$ ==================================================================
map

@$ ==================================================================
@$ Plot the results.
@$ Plotting this text is a bit complicated
@$ The Y coordinate must be in the units used to calculate the diagram (Kelvin)
@$ but the text will be plotted using the units for the plot (Celcius)
@$ This will eventually be corrected (until then edit the ocgnu.plt file)
@$ The lines with the same phase fix has the same color
@$ ==================================================================
plot w%(C) T_C
axis y Temperature/Celcius
@$ Label some areas (x, y, fontscale, angle, recalc, legend)
text 1.0 1100 0.9 0 Y ,
text n 1.0 1350 0.9 0 Y ,
text n 1.0 1500 0.9 0 Y ,
text n 0.4 1150 0.9 0 Y ,
text n 0.1 1300 0.9 0 Y ,
title map 16 Fig 1
render

@$ ====================================================================
@$ PDF output settings
@$ (graph n; n=1: window, n=2:ps, n=3:pdf, n=4:gif, n=5:png)
@$ ====================================================================
plot ,,
graph 3 phase-diagram-fix Y

@$ ==================================================================
@$ If you do not write this, you will get an error.
set interactive
@$ ==================================================================
